#! /usr/bin/env perl
use strict;
use warnings;

use Plack::Runner;
use Plack::Builder;
use Plack::App::Directory;
use YAML 'Dump';


sub main {
  # Taint.  /usr/bin/env can't do it for us.
  exec $^X, (map {( -I => $_)} @INC), -T => $0, @ARGV unless ${^TAINT};

  # app.psgi
  my ($root) = (shift @ARGV) =~ /^(.*)$/;  # untaint - we trust ARGV
  die "Syntax: $0 <webroot-dir> [... plackup args ]\n\nServe a directory via plackup.  root=$root\n"
    unless defined $root && -d $root && -r _;
  my $content = Plack::App::Directory->new({ root => $root })->to_app;

  # headers for pre-compressed files
  my $app = builder {
    enable 'Plack::MiddlewareX::PreZipped';
    enable 'Plack::MiddlewareX::HttpRangeBytes';
    $content;
  };

  # bin/plackup
  my $runner = Plack::Runner->new(app => $app);
  $runner->parse_options(@ARGV);
  $runner->run;

  return 0;
}

exit main();


BEGIN { $INC{'Plack/MiddlewareX/PreZipped.pm'} = $0; }
BEGIN { $INC{'Plack/MiddlewareX/HttpRangeBytes.pm'} = $0; }

package Plack::MiddlewareX::PreZipped;
use parent qw(Plack::Middleware);
use Plack::Util;
use YAML 'Dump';

### nginx.conf
#
#        location ~* "\.(json|txt)z$" {
#           add_header Content-Encoding  gzip;
#           gzip off;
#           types { application/json jsonz; }
#        }

sub call {
  my($self, $env) = @_;
  my $res  = $self->app->($env);
  if ($env->{REQUEST_URI} =~ m{\.(json|txt)z$}) {
    my $suffix = $1;
    return Plack::Util::response_cb($res, sub { $self->_fix_headers($suffix, @_) });
  } else {
    return $res;
  }
}

sub _fix_headers {
  my ($self, $suffix, $res) = @_;
  my $mime_type = Plack::MIME->mime_type(".$suffix");
  my ($status, $headers, $body) = @$res;
  $headers = Plack::Util::headers($headers);
#  warn Dump({ res_in => $res, _fix_headers => \@_ });
  $headers->set('Content-Encoding' => 'gzip');
  $headers->set('Content-Type' => $mime_type) if defined $mime_type;
  return;
}

1;


package Plack::MiddlewareX::HttpRangeBytes;
use parent qw(Plack::Middleware);
use Plack::Util;
use YAML 'Dump';

sub call {
  my($self, $env) = @_;
  my $res  = $self->app->($env);
  return Plack::Util::response_cb($res, sub { $self->_rangify(shift, $env->{HTTP_RANGE}) });
}

sub _rangify {
  my ($self, $res, $RANGE) = @_;
  my ($status, $headers, $body) = @$res;
  my $hout = Plack::Util::headers($headers);
  $hout->set('Accept-Ranges', 'bytes'); # per http://tools.ietf.org/html/rfc2616#section-14.5

  # If-Range: not handled (14.27)
  return unless $status == 200 && defined $RANGE;

  # Range: simple case only http://tools.ietf.org/html/rfc2616#section-14.35
  # XXX: multi-range and multipart/byteranges not handled (19.2)
  my ($start, $end) = $RANGE =~ m{^bytes=(\d+)-(\d+)$};
  if (!defined $start || !defined $end) {
    $self->_badrange($res, "Cannot parse HTTP_RANGE='$RANGE'");
  } elsif ($end-$start+1 > 30*1024*1024) { # arbitrary limit
    $self->_badrange($res, "Range request $RANGE is too long");
  } else {
    $self->_make_range($res, $hout, $body, $start, $end);
  }

  return;
}

sub _badrange {
  my ($self, $res, $msg) = @_;
  # whinge - probably violating http://tools.ietf.org/html/rfc2616#section-10.4.17
  warn "_badrange: $msg";
  @$res = (416, [ 'Content-Type', 'text/plain' ], [ "$msg\n" ]);
  return;
}

sub _make_range {
  my ($self, $res, $hout, $body, $start, $end) = @_;
  my $wantlen = $end - $start + 1;
  my $len;

  if (!ref($body)) {
    $len = length($body);
    if ($end + 1 > $len) {
      $self->_badrange($res, "Range $start-$end outside object of size $len");
    } else {
      $body = substr($body, $start, $end);
    }
  } elsif (Plack::Util::is_real_fh($body)) {
    my $fh = $body;
    $len = -s $fh; # XXX: hoping it is not a stream

    my $path = eval { $fh->can('path') } ? $fh->path : '[data]';
    if ($end + 1 > $len) {
      $self->_badrange($res, "Range $start-$end outside file size $len");
    } else {
      seek($fh, $start, 0) or die "Seek($path, $start, SEEK_SET): $!";
      my $nread = read($fh, $body, $wantlen);
      die "Read $path at $start+$wantlen: $!" unless defined $nread;
      die "Read $path at $start+$wantlen: want $wantlen, got $nread" unless $nread == $wantlen;
    }
  } else {
    die Dump({ body_type_unhandled => $body });
  }

  # selective compliance with http://tools.ietf.org/html/rfc2616#section-10.2.7
  my $len_sfx = defined $len ? "/$len" : '';
  $res->[0] = 206; # "Partial Content"
  $hout->set('Content-Length', $wantlen);
  $hout->set('Content-Range', "bytes $start-$end$len_sfx");
  $res->[2] = [ $body ];

  return;
}

1;
